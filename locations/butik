# butik
if $ARGS[0] = 'start':
	$metka = $ARGS[0]
	$loc = $CURLOC
	$metkaM = $ARGS[0]
	$locM = $CURLOC
	cla
	clr
	*clr
	mag = 1
	minut += 5
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
	'<center><b><font color="maroon">Boutique</font></b></center>'
	'<center><img src="images/etogame/butik.jpg"></center>'
	'All kind of dresses, all at 25,000 rubles'

	act 'Leave the boutique':
		cla
		minut += 5
		gt 'torgcentr'
	end

	if rand(1, 100) >= 80 and skin > 0 and lesbiQW = 0:
		'In the boutique you see a stylish and slender woman ,with about forty years old.'

		act 'Stare at the woman.':gt 'event', 'lesbi'
	end

	if QWdogreiqTimer = 0 and QWdogReiq = 1 and week = 7 and qwdogiventday ! day:
		qwdogiventday = day
		'Going into the boutique you begin to consider picking dresses on hangers.By casually taking one you try it on in passing it on your back ,you hang with mild grim face.One of the dresses unexpectedly attracts your attention , as you began to study it more closely ,you feel a strange feeling as if you were being watched'

		act 'Disregard':gt 'butik', 'start'
		act 'Look around':dynamic $butikreiqdi

		exit
	end

	if RimmaSex > 0:
		if rimmaday ! day:
			'<a href="exec:gt ''RimmaSexQW''">Rima</a> smiled at you. She bits her lip and starts shooting her eyes in your direction.'
		else
			'Rima smiled at you and continued to do her business.'
		end
	end

	if palto < 3:
		'The boutique sells a fur coat for winter - 50,000 rubles'

		if money >= 50000:
			act 'Buy a fur coat (50,000 rubles)':
				cla
				money -= 50000
				palto = 3
				$palto = 'fur coat'
				'You bought a fur coat.'

				act 'Leave':gt 'butik', 'start'
			end
		end
	end

	if money >= 25000:
		i = 1
		:loopdress
			if dress[i] = 0:dynamic 'act ''Look at dress <<i>>'':gt ''clothing'', ''view_clothing_item'', ''shop'', ''dress'', <<i>>, 25000'
			i += 1
		if i <= 23:jump 'loopdress'
	end
end

--- butik ---------------------------------
