# park
if $ARGS[0] = 'start':
	cla
	*clr
	gs 'parkivent'
	gs 'parkdin'

	$torchki = {
		cls
		gs 'stat'
		gs 'time'
		SNarkPriton = 1
		'The junkies look at you with a daunted gaze. They probably thought you are a police officer. Then one of them said, "We don''t sell no drugs. Go to the Drug House, ye can get zome there."'

		act 'Walk away from them':gt 'park', 'start'
	}

	$sexloc = $CURLOC
	minut += 30
	clr
	gs 'stat'
	gs 'time'
	'<center><b><font color="maroon">Park</font></b></center>'
	'<center><img src="images/etogame/park.jpg"></center>'
	'The central park is huge, sandy paths go into all directions and ever so often you''ll find a bench to sit on. In the summer you''ll see a lot people playing ball, relax or have a picnic in the more open areas, while in the winter you''ll meet families with their children sledding down the humps.'
	'Despite it''s almost paradise-like appearence, the park can be very dangerous once the sun goes down.'
	''
	'There is a sign that points into general direction of the <a href="exec:minut += 47 & gt ''down''">City Center</a>. You estimate the walk to the city center would take about 40 to 50 minutes.'
	'The second sign points into the direction of the <a href="exec:minut += 19 & gt ''street''">Residential Area</a>. If you have to guess, the walk to the residential are should take about 20 minutes.'
	'The third sign says <a href="exec:minut += 59 & gt ''Nord''">Northern District</a> on it and points north, you suppose. You know the Northern District is rahter far away from here, you estimate taht a walk would take almost one hour'
	''

	if SNarkPriton = 0:'Near one of the shops roll <a href="exec:dynamic $torchki">junkies</a>.'

	if car > 0 and cardrive = 5:
		cardrive = 5
		'Stands near the park <a href="exec:GS ''carF'', ''start''">your <<$car>></a>.'
	end

	if tanga = 0 and ski = 1 and hour > 6 and hour <= 20 or tanga = 0 and glamur = 1 and hour > 6 and hour <= 20:
		nurand = rand(0, 100)

		if nurand >= 80:
			manna -= 5
			SUB += 1
			horny += SUB
			nudity += 1
			'A man walks past you and notices that you didn''t put on any panties. He whistles at you, "Did you forget to put on your underwear today, honey?"'
			'You blush with shame and you try to walk out of his sight as fast as possible.'
		end
	end

	act 'Leave the park':
		cla
		minut += 30
		gt 'street'
	end

	if hour >= 10 and hour <= 22:
		act 'Sit in a CafÃ©':
			cla
			*clr
			minut += 30
			gt 'ParkKafe', 'food'
		end
	end

	act 'Walk in the park (1:00)':
		cla
		minut += 60

		if hour > 7 and hour < 21:
			parkrand = rand(0, 20)

			if parkrand < 10 and parkrand > 0:
				dynamic $parkrandivdin
				exit
			end

			if parkrand > 14:
				dynamic $parkrandivdin
				exit
			end
		end

		manna += 10
		willpower += 10
		gs 'stat'
		'You take a relaxing and refreshing walk through the park, trying to figure out the meaning of life.'

		act 'Finish the walk':gt 'park', 'start'
	end

	if pusher = 1 and hour >= 6 and hour <= 21:
		'There''s a drug dealer casually sitting on one of the benches. He''s trying not to be too suspicious.'

		if money >= 2000:
			act 'Buy "Pale Lady" (2,000 rubles)':
				cla
				minut += 10
				palelady = input ("How many doses you want to buy?")

				if palelady * 2000 > money or palelady <= 0:
					'Either tell me how many you want to buy or get lost.'
				else
					money -= palelady * 2000
					dur += palelady
					'You get out your purse and pay him quickly, hoping nobody else saw you giving him money. Then he passes you the doses you''ve paid for and you can savely sniff the stuff at home.'
				end

				act 'Casually walk away':gt 'park', 'start'
			end
		else
			'You do not have enough money, not even for one dose.'
		end
	end

	if sport > 0 and parkrunday ! day:
		act 'Jog through the park (1:00)':
			cla
			*clr
			parkrunday = day
			minut += 30
			manna -= 10
			willpower -= 10
			beg += 1
			fat -= 8
			speed += 1
			sweat += 3
			'<center><img src="images/etogame/parkbeg.jpg"></center>'
			'You randomly take the paths, you run them up and down and about an hour later, you are out of breath and heavily sweating.'

			if hour >= 20:
				parkrand = rand(0, 10)

				if parkrand >= 8:
					'A shade steps out of the shadow from a tree and runs up to you. Before you can react, he hits you and drags you into the bushes. "Relax and enjoy this", he says.'

					act 'Fight':
						cla
						$nameV = 'Rapist'
						!!stats
						strenV = 100
						speedV = 50
						agilV = 40
						reactV = 40
						vitalV = 50
						intelV = 10
						willV = 10
						!!skills
						magikV = 100
						boxingV = 50
						shootV = 100
						!!parameters
						healthV = vitalV * 10 + strenV * 5
						mannaV = intelV * 10 + magikV * 10
						willpowerV = willV * 10
						BonusAtakV = 0
						BonusDefV = 0
						!!Programme of Action
						program = 1
						RapistFight = 1

						if spellavtoklon = 1:klon = 3
						if spellbefshild = 1:defence = 500

						gt 'fight', 'start'
					end

					exit
				end
			end

			act 'Complete jog':gt 'park', 'start'
		end
	end

	act 'Go to the highway near the park':gt 'park', 'whore'
	act 'Go to the fair':gt 'park', 'luna'
end

if $ARGS[0] = 'luna':
	cla
	*clr
	minut += 5
	clr
	gs 'stat'
	gs 'time'
	'<center><b><font color="maroon">The Fun Fair</font></b></center>'
	'<center><img src="images/etogame/luna.jpg"></center>'
	'The fun fair sits in the hearts of the park and attracts families, people on a vacation and loving couples from all over the town. It''s a popular gathering spot and a place to forget your worries for a few hours.'

	if vladimirQW = 0 and hour >= 9 and hour <= 20 and week > 5 and vnesh > 10:'<a href="exec:gt ''qwloc''">There''s a strong looking man staring at you from the fences.</a>.'


	if hour >= 9 and hour <= 20:
		act 'Distribute flyers to earn money (50 rubles) (1:00)':
			cla
			*clr
			minut += 60
			money += 50
			manna -= 5
			'<center><img src="images/etogame/flaer.jpg"></center>'
			'For an hour straight, you walked through the park and the fun fair, chatting up random visitors and trying to give them one of your flyers. After that you are being paid 50 rubles for your effort.'

			act 'Move away':gt 'park', 'luna'
		end

		if money >= 50:
			act 'Eat Shawarma (50 rubles)':
				cla
				*clr
				minut += 30
				health += 10
				money -= 50
				manna += 5
				willpower += 5
				'<center><img src="images/etogame/luna1.jpg"></center>'
				if energy >= 30:
					fat += 7
					'You can''t eat one more bite.'
				elseif energy >= 20 and energy < 30:
					fat += 5
					energy += 10
					'While you weren''t really hungry, you ate the entire dish of shawarma.'
				elseif energy < 20:
					energy += 20
					fat += 3
					'You are glad you finally got something to eat.'
				end

				act 'Move away':gt 'park', 'luna'
			end

			act 'Drink Lemonade (50 rubles)':
				cla
				*clr
				minut += 30
				health += 10
				money -= 50
				manna += 5
				willpower += 5
				'<center><img src="images/etogame/luna1.jpg"></center>'
				if water >= 20:
					fat += 3
					'You weren''t really thristy, but you still emptied the bottle of lemonade.'
				elseif water < 20:
					fat += 2
					'Finally you got something to drink, even if it''s just a bottle of lemonade.'
					water += 20
				end

				act 'Move away':gt 'park', 'luna'
			end

			act 'Ride on the carousel (50 rubles)':
				cla
				*clr
				minut += 30
				money -= 50

				if will < 30:will += 1
				if will >= 30:will += rand(0, 1)

				willpower += 10
				manna += 10
				'<center><img src="images/pic/karusel.jpg"></center>'
				'You paid 50 rubles and sat in the carousel.'
				'You have a great ride, already dazzled.'

				act 'move_away':gt 'park','luna'
			end
		end

		if money >= 100:
			act 'Shoot some targets (100 rubles)':
				cla
				*clr
				minut += 30
				money -= 100
				'<center><img src="images/etogame/tir.jpg"></center>'
				'You paid 100 rubles for ten bullets. If you are able to hit all targets, you can win a teddy bear.'

				act 'Shoot':
					cla
					tirkoef = agil + shoot
					tirmin = tirkoef * 20 / 100
					tirand = rand(tirkoef - tirmin, tirkoef + tirmin)

					if tirand >= 80:
						mishka += 1
						manna += 5
						'You hit all targets and didn''t even miss once. Congratulations! You''ve won a Teddy Bear.'
					elseif tirand < 80 and tirand >= 70:
						'Oh, you''ve only missed once.'
					elseif tirand < 70 and tirand >= 60:
						'You were able to hit the target eight times, but you''ve missed twice.'
					elseif tirand < 60 and tirand >= 50:
						'You were able to hit the target seven times, but you''ve missed twice.'
					elseif tirand < 50 and tirand >= 40:
						'Six times you were able to hit the target, but you''ve missed four times.'
					elseif tirand < 40 and tirand >= 30:
						'Five times you were able to hit the target, but you''ve missed five times too.'
					elseif tirand < 30 and tirand >= 25:
						'Four times you were able to hit the target, but you''ve missed six times.'
					elseif tirand < 25 and tirand >= 20:
						'Three times you were able to hit the target, but you''ve missed seven times.'
					elseif tirand < 20 and tirand >= 15:
						'You were able to hit the target twice, but you''ve missed it eight times.'
					elseif tirand < 15 and tirand >= 10:
						'You were only able to hit the target once and missed it nine times.'
					elseif tirand < 10:
						'You couldn''t even hit the target once.'
					end

					shoot += 1

					act 'Move away':gt 'park', 'luna'
				end
			end
		end

		if money >= 20:
			act 'Hit the dynamoeter (20 rubles)':
				cla
				*clr
				'<center><img src="images/etogame/silomer.jpg"></center>'
				'You''ve paid 20 rubles to play Ring-The-Bell.'

				act 'Bump':
					cla
					minut += 30
					money -= 20
					stKo = stren * 20/100
					damage = rand(stren - stKo, stren + stKo)

					if stren < 30:stren += 1
					if stren >= 30:stren += rand(0, 1)

					'You swung the hammer as hard as you could, but it wasn''t enough for a new record. A LED screen lights up and shows you your score:<<damage>>'

					if damage > maxdamage:maxdamage = damage & 'Congratulations! You''ve just a new record!'

					act 'Move away':gt 'park', 'luna'
				end
			end
		end

		act 'One-Armed Bandit':gt 'park', 'avtomat'
	end

	act 'Go back to the park':gt 'park', 'start'
end

if $ARGS[0] = 'avtomat':
	cla
	*clr
	clr
	gs 'stat'
	gs 'time'
	'<center><img src="images/etogame/avtomat.jpg"></center>'
	'The rules of the game are very simple, throw 5 rubles into the slot, pull the lever and with a bit of luck you can win the jackpot of 1.000 rubles.'

	if money >= 5 and hour <= 20:
		act 'Throw in 5 rubles and pull the lever':gt 'park', 'avtomat2'
	end

	act 'Move away from the slot machine':gt 'park', 'luna'
end

if $ARGS[0] = 'avtomat2':
	cla
	*clr
	clr
	gs 'stat'
	gs 'time'
	minut += 5
	money -= 5
	avtrand = rand(0, 100)

	if avtrand = 100:
		money += 1000
		'Jackpot! You win 1,000 rubles.'
	elseif avtrand < 100 and avtrand >= 95:
		money += 100
		'You win 100 rubles.'
	elseif avtrand < 95 and avtrand >= 85:
		money += 10
		'You win 10 rubles.'
	elseif avtrand < 85 and avtrand >= 75:
		money += 5
		'You win 5 rubles.'
	elseif avtrand < 75:
		'Unfortunately, you weren''t able to win anything this time.'
	end

	act 'Try your luck once more':gt 'park', 'avtomat'
end

if $ARGS[0] = 'whore':
	cla
	*clr
	minut += 5
	clr
	gs 'stat'
	gs 'time'
	'<center><b><font color="maroon">The road near the park</font></b></center>'

	if hour >= 16 and hour <= 23:
		'<center><img src="images/etogame/park1.jpg"></center>'
		'Along the roadside <a href="exec:gt ''prostitute'', ''start''">Prostitutes</a> are walking and sometimes picked up by a car.'
	elseif hour >= 4 and hour < 16:
		'<center><img src="images/etogame/glpark.jpg"></center>'
		'Cars passing on the road past the park and there is not seen nothing interesting.'
	else
		! WD: Remove Dupe Images ~ '<center><img src="images/pic/park1.jpg"></center>'
		'<center><img src="images/etogame/park1.jpg"></center>'
		'Cars drive on the road past the park and then not see anything interesting.'
	end

	temp = rand(1, 10)

	if temp > 5 and exhibi > 2:
		act 'Walking in pantyhose':
			cla
			*clr
			set minut = minut + 30
			'<center><img src="images/etogame/parkex.jpg"></center>'
			'You shoot the robe and walk into some pantyhose, as though-forgot to wear a skirt.'
		end
	end
	act 'Go back to the park':gt 'park', 'start'
end

if $ARGS[0] = 'prost':
	cla
	*clr
	minut += 5
	clr
	gs 'stat'
	gs 'time'
	'<center><b><font color="maroon">Hooker</font></b></center>'
	'<center><img src="images/etogame/park2.jpg"></center>'

	if whoreQW = 2:
		'Prostitutes look at you with curiosity.'
	elseif whoreQW = 1:
		'Prostitutes look at you with interest.'
		act 'Talk':
			cla
			whoreQW = 2
			'You went to a prostitute and asked her how things are done here at all. She began to explain. "We are free, do not work on anyone, but because of this many problems. Svimi Stella with her friends sometimes beat us and rob and sometimes come petty thugs and rob us of the money. If you want to work here, you have to pay and Stella and Bandyuk . course you cool, you can try to besiege Stella and her friends, but the bandits do not advise contact. And yet, you do not peck on customers, if you nebudesh dressed like a slut, you can buy some clothes in a sex shop. "'

			act 'Move away':gt 'park', 'prost'
		end
	elseif whoreQW = 0:
		'Hey, you''re not one of us, would valyl from our point, and that the teeth will not collect.'

		act 'Fuck you!':
			cla
			minut += 15
			$nameV = 'Stoned prostitute'
			!!stats
			strenV = 20
			speedV = 30
			agilV = 30
			reactV = 30
			vitalV = 50
			intelV = 10
			willV = 10
			!!skills
			magikV = 10
			boxingV = 10
			shootV = 10
			!!parameters
			healthV = vitalV * 10 + strenV * 5
			mannaV = intelV * 10 + magikV * 10
			willpowerV = willV * 10
			BonusAtakV = 0
			BonusDefV = 0
			!!Programme of Action
			program = 0
			prostitutFight = 1

			if spellavtoklon = 1:klon = 3
			if spellbefshild = 1:defence = 500

			gt 'fight', 'start'
		end
	end
	if whoreQW > 1 and $clothingworntype = 'hooker':
		act 'Search client (0:15)':
			cla
			*clr
			minut += 15
			whrand = rand(0, 100)

			if whrand <= 5:
				'To you black jeep pulled up, opened the window and leaned out healthy brazen face. "Hey slut, it''s time to pay tribute. Shtukar Come here and get out on work."'

				if money >= 1000:
					act 'Giving money (1,000 rubles)':
						cla
						money -= 1000
						'You gave the money big man in the jeep. He put them in his pocket and left.'

						act 'Move away':gt 'park', 'prost'
					end
				end

				act 'Fuck you!':
					cla
					minut += 15
					$nameV = 'Bandit'
					!!stats
					strenV = 200
					speedV = 30
					agilV = 20
					reactV = 20
					vitalV = 150
					intelV = 10
					willV = 10
					!!skills
					magikV = 10
					boxingV = 10
					shootV = 10
					!!parameters
					healthV = vitalV * 10 + strenV * 5
					mannaV = intelV * 10 + magikV * 10
					willpowerV = willV * 10
					BonusAtakV = 0
					BonusDefV = 0
					!!Programme of Action
					program = 0
					prostitutFight = 2

					if spellavtoklon = 1:klon = 3
					if spellbefshild = 1:defence = 500

					gt 'fight', 'start'
				end
			elseif whrand > 5 and whrand <= 60:
				minut += 15
				bj += 1
				guy += 1
				money += 500
				throat += 1
				'<center><img src="images/picV/car.jpg"></center>'
				'To you a car drove up and a man sitting behind the wheel asked how much it would cost the vacuum. You called a standard fee of 500 rubles. He agreed and opened Passenger door for you.'

				if prezik > 0:'You got a condom and put it in your mouth, then bent over his pants and put his client condom on the penis with his mouth.'

				'You clasped lips member client and began to suck it, caress the tongue and try to take it as deep as possible.'

				if prezik = 0:
					swallow += 1
					cumlip += 1
					'Client started cumming in your mouth, you continued to suck and swallow his hot, tart sperm.'
					gerprand = rand(0, 100)

					if gerprand > 96:
						if VeneraOnce = 0:VeneraOnce = 1
						Venera += 1
					end
				elseif prezik > 0:
					prezik -= 1
					'Client finished in a condom.'
				end

				'After the client has finished you off immediately dropped out of the car.'

				act 'Go to the panel':gt 'park', 'prost'
			elseif whrand > 60 and whrand <= 90:
				minut += 15
				'You were walking along the sidewalk showing off their charms completely in vain, no you are not interested.'

				act 'Go to the panel':gt 'park', 'prost'
			elseif whrand > 90:
				'To you approached a prostitute. "I hear you, slut, if you do not want problems, you have to pay Stella. Pyatihatnik Come here and get out on work."'

				if money >= 500:
					act 'Giving money (500 rubles)':
						cla
						money -= 500
						'You gave the money to the prostitute, she turned and walked away.'

						act 'Move away':gt 'park', 'prost'
					end
				end

				act 'Fuck you!':
					cla
					minut += 15
					$nameV = 'Prostitute'
					!!stats
					strenV = 50
					speedV = 50
					agilV = 40
					reactV = 30
					vitalV = 60
					intelV = 10
					willV = 10
					!!skills
					magikV = 10
					boxingV = 10
					shootV = 10
					!!parameters
					healthV = vitalV * 10 + strenV * 5
					mannaV = intelV * 10 + magikV * 10
					willpowerV = willV * 10
					BonusAtakV = 0
					BonusDefV = 0
					!!Programme of Action
					program = 0
					prostitutFight = 3

					if spellavtoklon = 1:klon = 3
					if spellbefshild = 1:defence = 500

					gt 'fight', 'start'
				end
			end
		end
	end

	act 'Move away':gt 'park', 'start'
end

if $ARGS[0] = 'rape1':
	cla
	*clr
	rape += 1
	sex += 1
	SUB += 3
	money = 0
	'<center><img src="images/img/spaln/rape.jpg"></center>'
	'Suddenly, you are hit on the head. You awake sometime later in a bush. Somehow you''re not able to move your arms, they must be tied up. As you look down on yourself, you realize, that you''re not wearing anything and before you can even react some dude is ramming his into your cunt. "Shit", you think yourself, "I''m being raped."'

	act 'Try to resist':
		cla
		cumpussy += 1
		'You try everything that''s possible in your situation right now to resist him, but he just tightens his grips and doesn''t stop fucking you. Some time later he begins to groan and you can feel his hot cum flowing inside your pussy. You just hope to yourself, that he didn''t impregnate you. He then zips up his pants, takes your money and leaves.'

		act 'Leave':gt 'park', 'start'
	end

	act 'Try to relax and enjoy it':
		cla
		*clr
		anal += 1
		SUB += 5
		'<center><img src="images/img/spaln/rapean.jpg"></center>'
		'It doesn''t take long for the rapist to realize you''re not resisting. "Good girl!", he says to you. He pushes you onto your knees and intends to take you doggy-style. He spreads your asschecks aside and pushes his cock inside. You cry out in pain, but he holds your mouth shut and doesn''t stop fucking your asshole.'

		act 'Go on':
			cla
			*clr
			bj += 1
			cumlip += 1
			cumface += 1
			SUB += 5
			'<center><img src="images/img/spaln/cum.jpg"></center>'
			'"Give me a blowjob and you''ll get your reward for not resisting, woman.", he tells you. You nod obidiently and move on your knees and begin to give him head. It doesn''t take long for him to groan out in pleasure, "You better swallow it all, slut!" Shortly after that you can taste his hot cum inside your mouth and you swallow every drip of it, somehow enjoying the taste. Then he stand up, buttons up his pants and takes your money. While you were still liking your fingers clean from any drips of cum, he smiles and says to you, "Have a nice day slut."'

			act 'Stand up and leave':gt 'park', 'start'
		end
	end
end

--- park ---------------------------------
