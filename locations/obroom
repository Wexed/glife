# obroom
if $ARGS[0] = 'start':
	$sexloc = $CURLOC
	$metkaM = $ARGS[0]
	$locM = $CURLOC
	cla
	*clr
	odkomp = 1
	gs 'stat'
	'<center><b>Your room</b></center>'
	'<center><img src="images/img/centr/obroom.jpg"></center>'
	'This is your room, with you in it lives another girl Vika'

	if hour > 14 and hour < 20 and vikaend = 0:'Lying on my bed your neighbor <a href="exec:gt ''obroom'', ''vika''">Wick</a>.'
	if hour > 3 and hour < 8 and vikaslut = 0:'Wick is sleeping in her bed after a night of being like. I wonder where it disappears at night.'
	if hour > 3 and hour < 8 and vikaslut = 1:'Wick is sleeping in her bed, after work in a brothel.'
	if komp = 1:'<a href="exec:gt ''Komp'', ''start''">Computer</a>.'

	act 'In the corridor':
		if nude = 0:
			odkomp = 0
			minut += 5
			gt 'dorm', 'korr'
		else
			'You need to dress'
		end
	end

	act 'Learn lesson (2:00)':
		cla
		minut += 120
		intel += 1
		'You prozanimavshis two hours seems something in your head postponed'

		act 'Finish':gt 'obroom', 'start'
	end

	if student > 0 and kursovik < 100:
		act 'Write kursovik (1:00)':
			cla
			*clr
			minut += 60
			manna -= 10
			kursovik += rand(intel / 20, intel / 10)

			if kursovik > 100:kursovik = 100

			'<center><img src="images/etogame/study.jpg"></center>'
			'You sat on the course work and prepared her <<kursovik>> percent.'

			act 'Finish':gt 'obroom', 'start'
		end
	end

	act 'Mirror':
		obMir = 1
		gt 'mirror', 'start'
	end

	act 'Cupboard':
		obLoker = 1
		gt 'loker', 'start'
	end

	act 'In bed':
		houseob = 1
		gt 'bed', 'start'
	end

	if tanga = 0 and clrbelo > 0:
		act 'Wear underwear':
			cla
			tanga = 1
			gt 'obroom', 'start'
		end
	else
		act 'Remove Underwear':
			cla
			tanga = 0
			gt 'obroom', 'start'
		end
	end
end

if $ARGS[0] = 'vika':
	cla
	gs 'stat'
	'<center><img src="images/img/centr/kat.jpg"></center>'

	act 'Leave':gt 'obroom', 'start'

	if hour < 20:
		act 'Chat with Vika (1:00)':
			cla
			minut += 60
			vika += 1
			talkrand = rand(0, 10)

			if talkrand = 0:'You started talking with Vika, she tells you about the boys she met, about parties, about cosmetics.'
			if talkrand = 1:'Wick says she was in a cafe and invited her to dance even then the guy who paid her bill.'
			if talkrand = 2:'Wick says she worked at a strip club dancer, quite lucrative job for students, although customers are very pristavuchi.'
			if talkrand = 3:'Vic tells you that several sections of beautiful girls running at the university entered the European level sports competitions and earn big money, but of course that Vick priznaetsya her laziness so they plow.'
			if talkrand = 4:'Vick says that one of her friends flew from a guy who did not have time to pull out of it. But Vick smarter, she buys in a pharmacy contraceptives, 100% satisfaction guarantee.'
			if talkrand = 5:'Vick says in the park, she accidentally went to the panel and she was offered sex for money.'
			if talkrand = 6:'Vic tells how she acted naked in studio, not a dusty job, but reputation suffers.'
			if talkrand = 7:'Vic tells a horror story about how one pretty girl did not wash his feet and did not shave, then became so terrible to her for all obbegat kilometer.'
			if talkrand = 8:'Vic says that she buys tampons always with a reserve, and then if they go without a monthly then quickly prevratishsya in slut.'
			if talkrand = 9:'Vic tells how one of her guys wanted anal sex, but she was not in possession of Vaseline.'
			if talkrand = 10:'Vick says the summer sunning herself on the beach, for this truth had her buy a swimsuit, but it came in handy in the winter and go to the pool. The pool is not a bad tool to maintain a healthy body.'

			act 'Finish':gt 'obroom', 'vika'
		end

		act 'Engage with Vika (1:00)':
			cla
			minut += 60
			intel += 1
			vika += 1
			'You overlaid textbooks and become engaged with Vika, reviews all absorbed twice as fast.'

			act 'Finish':gt 'obroom', 'vika'
		end

		if vikasex > 0 and vika > 20:
			act 'Offer fun':
				minut += 60
				gt 'lezbsex', 'start'
			end
		end

		if vika > 70 and vikaslut = 0:
			act 'Ask where it is at night':
				cla
				minut += 30
				vikaslut = 1
				'Vick hesitated and blushed, "I just walk."'
				'"Yeah, she walks at night in the street, and I''ll believe it, kolis friend where you are wearing." neunimalis you.'
				'"I work at the" Vic mumbled unsure.'
				'"Many times I was in the club and you were not vidila, enough already lie." you misbehaved, and a little later added, "Do not want to not say it is not very much and wanted." and turned away from her.'
				'"Well do not be mad, I''ll show you just promise me that you then at least you will talk to me"'
				'You were taken aback by these words than she can do, that you stop talking to her, not with the same dogs publicly fucked. "Yes, of course I''m going to talk to you, what a stupid question."'
				'"Good show went"'

				act 'Go with Vika':gt 'bordelv', 'start'
			end
		elseif vika > 70 and vikaslut = 1 and bordel = 0:
			act 'Ask about work':
				cla
				vika += 5
				bordel = 1
				odkomp = 0
				'"And I have arranged to work in a brothel, I know how much." you asked.'
				'"Well I''ll tell the administrator, but I can not guarantee it can not take."'

				act 'Finish':gt 'obroom', 'vika'
			end
		end
	end
end

--- obroom ---------------------------------
