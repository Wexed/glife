# gpoli
clr
minut += 5
frost = 0
gs 'stat'
fcolor = rgb(0, 0, 0)
bcolor = rgb(255, 255, 255)
lcolor = rgb(106, 90, 205)
'<center><b><font color="maroon">Clinic</font></b></center>'
'<center><img src="images/etogame/poli.jpg"></center>'

act 'Leave':gt 'gorodok'

if hour >= 6 and hour <= 21:
	if gpoliuborka = 0:
		act 'Read listing':
			cla
			*clr
			minut += 5
			gpoliuborka = 1
			'On the wall near the entrance to the clinic hanging ad. Requires cleaner, work one hour a day, from 16 to 21 hours, payment of 25 rubles.'

			act 'Move away':gt 'gpoli'
		end
	elseif gpoliuborka = 1 and hour >= 8 and hour <= 21:
		act 'Ask the chief doctor of the work as a cleaner':
			cla
			*clr
			gpoliuborka = 2
			minut += 15
			gs 'stat'
			'<center><img src="images/etogame/poli.jpg"></center>'
			'You came to the office of the head doctor and knocked on the door entered the office. In the office sat a large man in a white robe. He looked sternly at you through his glasses. "On what issue?" You answered that you have read the ad and would like to get a cleaner. Heads doctor thought for a moment and handed you the paper. "Write on the model application for a job part-time." You took the paper and wrote a statement. Heads doctor stood up together and you went into the corridor. He showed you the shed where the stored tools, buckets, mops, dusters.'

			act 'Leave':gt 'gpoli'
		end
	elseif gpoliuborka > 1 and hour >= 8 and hour <= 21 and gpolicleanday ! day:
		act 'To work as a cleaner':
			cla
			*clr
			gpolicleanday = day
			minut += 60
			manna -= 15
			money += 100
			sweat += 2
			gs 'stat'
			'<center><img src="images/etogame/clener1.jpg"></center>'
			'You took a mop bucket and washed corridors throughout the clinic. After washing corridors you steel wash rooms. Doctors certainly cursed and bubneli as you unceremoniously broke into their offices, but still give you a calm, clean the floor.'

			act 'Finish the job':gt 'gpoli'

			ginrand = rand(0, 1)

			if Gspravka < 10 and ginrand = 0:
				'When you remove the gynecologist office, he goes out of the room leaving you alone.'

				if vagina > 0:
					act 'Fake certificate':
						cls
						Gspravka = 30
						GspravkaT = 1
						gs 'stat'
						'You quickly pull out of the table form and print reference and spank seal on the certificate.'
						'After that, you take away the seal back to the table and hide help. Help text from the gynecologist you write a little later on.'

						act 'Finish the job':gt 'gpoli'
					end
				end
			end
		end
	end

	if health < (vital * 10 + stren * 5) and money >= 1000:
		act 'Treat health (1,000 rubles)':
			cla
			money -= 1000
			minut += 60
			health = vital * 10 + stren * 5 + 1000
			'The doctor did give you a shot and you immediately felt much better'

			act 'Leave':gt $curloc
		end
	end

	if willpower < intel * 5 + will * 5 and money >= 1000:
		act 'Receiving a psychotherapist (1,000 rubles)':
			cla
			money -= 1000
			minut += 60
			willpower = intel * 5 + will * 5 + 1000
			manna = (intel * magik) + magik * 100 + vital * 10 + rikudo
			gs 'stat'
			'You lie down on a couch at the therapist and told him about his troubles, and you immediately become much happier.'

			act 'Leave':gt $curloc
		end
	end

	if sick >= 1:
		act 'Go to the therapist uchastkovumu':
			cla
			minut += 15
			sick = 0
			gs 'stat'
			'You came to the reception to uchastkovumu therapist and doctor has prescribed pills for colds.'

			act 'Leave':gt $curloc
		end
	end

	if StoryLine > 0 and SchoolAtestat = 0 and Gspravka < 3:
		act 'Go to the gynecologist for help':
			cls
			minut += 15
			'<center><img src="images/etogame/gin.jpg"></center>'
			'You lay on the examination table and pushed his feet. Gynecologist began to examine your vagina.'

			if vagina > 0:
				Gspravka = 30
				GspravkaT = 2
				'Help gynecologist wrote that you are not a virgin.'
			elseif vagina = 0:
				Gspravka = 30
				GspravkaT = 1
				'Help gynecologist wrote that you are a virgin.'
			end

			if preg > 0:'Gynecologist told you that you are pregnant.'

			gs 'stat'

			act 'Leave':gt $curloc
		end
	end
else
	'Clinic is closed.'
end

--- gpoli ---------------------------------
