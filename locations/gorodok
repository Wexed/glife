# gorodok
$metka = $ARGS[0]
$loc = $CURLOC
$metkaStreet = $ARGS[0]
$locStreet = $CURLOC
cla
*clr
litltown = 1
alkoblock = 0
boynumBlock = 0
music_on = 0

'<center><h4>The Village</h4></center>'
'<center><img src="images/etogame/gorodok.jpg"></center>'
'A quiet province village, if you had no idea of you where abouts, you couldn''t differ this village from others. It''s tallest building is the chuches tower.'

if GorSlut = 6 and rand(1, 20) = 20:
	'To you a guy came up and offered to move into the bushes to get laid for a hundred.'

	act 'Agree':gt 'gevent', '5'
	act 'Refuse':gt $curloc
	exit
end

if AlbFather >= 1:
	if AlbFaterBlackTime = 0:AlbFaterBlackTime = totminut + 2880

	'You can see voting posters on the walls. They say, "Vote for Boris Barlovsky!".'
end

if AlbFaterBlackTime > 0 and AlbFaterBlackTime <= totminut and hour >= 16 and AlbFather = 1:gt 'AlbFatherQW'
if hour >= 16 and hour <= 22 and AlbFather = 2 and AlbZaDay + 30 < daystart and year > 2010:
	gt 'AlbStarsEnd'
end

if StoryLine > 0 and hour < 5 and motherWorry = 0 and SchoolAtestat = 0 and age < 18:
	motherWorry = 1
end

if NosovRevenge = 2 and daynosovrevenge + 1 >= daystart:
	cls
	gs 'stat'
	'<center><b><font color="maroon"><<$npcName[1]>> <<$npcSurName[1]>></font></b></center>'
	'<center><img src="images/qwest/alter/npc/1.jpg" ></center>'
	'Near the house "Well slut. If you do not want that to video became public, Now let''s go and talk to me."'

	act 'Go to Dimka':
		cls
		NosovRevenge = 3
		minut += 30
		gs 'stat'
		'You went to the house of Dimka. The house where he lives belongs to his parents, this is a big two-storey, detached house with a manicured lawn and a large garden. With Dimka you have gone to his room and he closed the door with a key.'

		act 'Go into the room Dima':gt 'dimaHome'
	end

	exit
end

if rand(1, 5) >= 4:
	if GorSlut = 3:'You catch yourself on people''s views, some are trying to find you in someone, and maybe learn. Occasionally you hear people chuckle. Looks like someone found out about your adventures.'
	if GorSlut = 4:'People look at you with a grin, someone makes vulgar gestures towards you some hints on your adventures.'
	if GorSlut >= 5:'All around you will recognize whore. Some came by and slap you on the ass. Grandmother on the benches name-calling you whore.'
end

gs 'stat'
gs 'time'

if car > 0 and cardrive = 20:
	cardrive = 20
	'In the parking stands <a href="exec:gs ''carF'', ''start''">your <<$car>></a>.'
end

if StoryLine = 1 and ParHomeBlock = 0:'Your home is in one of the houses, it''s an <a href="exec:minut += 15 & gt ''korrPar''">apartment</a> your parents own.'

'The <a href="exec:minut += 15 & gt ''vokzalG''">Train Station</a>.'
'The <a href="exec:minut += 15 & gt ''gpoli''">Clinic</a>.'

if hour >= 8 and hour <= 20:'The <a href="exec: minut += 15 & gt ''gaptek'', ''start''">Pharmacy</a>.'
if hour >= 8 and hour <= 17:'<a href="exec: minut += 3 & gt ''barbershop'', ''start''">The local barbershop</a>.'
if hour >= 8 and hour <= 22:'A kiosk selling <a href="exec:dynamic $kiosksig">cigarettes</a>.'

'<a href="exec:gt ''zaprF'', ''start''">Gas Station</a>.'
'<a href="exec:gt ''gdk''">House of Culture</a>.'

if SchoolBlock = 0:'Local <a href="exec:gt ''gschool''">school</a>.'
if hour >= 7 and hour <= 18:'The <a href="exec:gt ''etoexhib'', ''pos20''">Vocational School</a>.'
if hour >= 8 and hour <= 20:'The only major store in town is the <a href="exec:gt ''shop'', ''start''">supermarket</a>.'

'Small city <a href="exec:gt ''gskver'', ''park''">park</a>, in which people are going.'
'Aside from residential buildings is <a href="exec:minut += 5 & gt ''garmassiv''">Garage array</a>.'

if ArtemBeInHome > 0 and artemblok = 0:'Apartment <a href="exec:minut += 15 & gt ''artemhome''">Artem Chebotarev</a>.'
if DimaBeHomeOnce = 1 and DimaRudeBlock = 0:'Private house in which he lives <a href="exec:minut += 15 & gt ''dimaGoHome2''">Dimka Nosov</a>.'
if katjaFirstKiss >= 1:'Private house in which <a href="exec:minut += 15 & gt ''KatjaHome''">Katja Meynold</a> lives.'
if BeInNatBelHome = 1 and hour >= 18 and hour <= 20 and week < 6 or hour >= 8 and hour <= 20 and week >= 6:'The house where the apartment <a href="exec:minut += 15 & gt ''NatBelHome''">Natasha Belova</a>.'
if BeInJulMilHome = 1 and hour >= 18 and hour <= 20 and week < 6 or hour >= 8 and hour <= 20 and week >= 6:'The house where the apartment <a href="exec:minut += 15 & gt ''JuliaMilHome''">Julia Milova</a>.'
if beinalbinahome > 0:'Luxurious mansion in which he lives <a href="exec:minut += 15 & gt ''albinaporog''">Albina Barlovskaya</a>.'

'Urban development enterprise <a href="exec:minut += 15 & gt ''Gshveyfab''">Garment factory named Paris Commune</a>.'

if workPTU > 2:act '<b>Go to the hostel</b>':gt 'etoexhib', 'pos40'

act 'Wait':
	cla
	waiting = input ("How long would you like to wait? (Not more than 120 minutes)")

	if waiting <= 0 or waiting > 120:
		minut += 15
	elseif waiting > 0 and waiting <= 120:
		minut += waiting
	end

	act 'Take a look at the clock':gt $curloc
end

if StoryLine = 1 and hour >= 8 and hour < 23 and cumface > 0 and rand(0, 3) = 0 and sosedday ! day:
	cls
	sosedday = day
	gs 'stat'

	if sosedknow = 0:
		sosedknow = 1
		'Suddenly someone call out to you "<<$name>>." You turn around and see a neighbor Uncle Misha, he looks at your face with surprise "It''s that on your face?" You blush and hesitate, Stra hand to wipe off the face of sperm. he grins "Well, all clear." and goes on about his business.'
	else
		'Once again you are confronted face to face with Uncle Misha, your neighbor, he chuckled looking at you "Well."'
	end
	act 'Further':gt $curloc
	exit
end

--- gorodok ---------------------------------
