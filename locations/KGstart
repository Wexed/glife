# KGstart
$KGOLklassvibor = {
	cls
	'You must select a character class.'

	if KGOLrace = 1:
		'For people available classes:Warrior, Thief, Mage.'

		act 'Warrior':$KGOLklass = 'Warrior' & KGOLklass = 1 & dynamic $KGOLklassvibor2
		act 'Thief':$KGOLklass = 'Thief' & KGOLklass = 2 & dynamic $KGOLklassvibor2
		act 'Magician':$KGOLklass = 'Magician' & KGOLklass = 3 & dynamic $KGOLklassvibor2
	elseif KGOLrace = 2:
		'For elves available classes Priest, Archer.'

		act 'Priest':$KGOLklass = 'Priest' & KGOLklass = 4 & dynamic $KGOLklassvibor2
		act 'Archer':$KGOLklass = 'Archer' & KGOLklass = 5 & dynamic $KGOLklassvibor2
	else
		'Classes available for zveroidov Barbarian.'

		act 'Barbarian':$KGOLklass = 'Barbarian' & KGOLklass = 6 & dynamic $KGOLklassvibor2
	end
}

$KGOLklassvibor2 = {
	cls
	KGOLexp = 0
	KGOLlvl = 1
	KGOLbrona = 0
	KGOLwpower = 0
	'<<$KGOLname>> <<$KGOLrace>> <<$KGOLklass>> <<KGOLlvl>> level'
	if KGOLklass = 1:
		KGOLstren = 30
		KGOLspeed = 20
		KGOLvital = 30
		KGOLintel = 20
	elseif KGOLklass = 2:
		KGOLstren = 20
		KGOLspeed = 30
		KGOLvital = 20
		KGOLintel = 30
	elseif KGOLklass = 3:
		KGOLstren = 10
		KGOLspeed = 20
		KGOLvital = 20
		KGOLintel = 50
	elseif KGOLklass = 4:
		KGOLstren = 10
		KGOLspeed = 10
		KGOLvital = 10
		KGOLintel = 70
	elseif KGOLklass = 5:
		KGOLstren = 20
		KGOLspeed = 40
		KGOLvital = 10
		KGOLintel = 30
	else
		KGOLstren = 40
		KGOLspeed = 10
		KGOLvital = 40
		KGOLintel = 10
	end

	KGHP = KGOLvital * 10
	KGManna = KGOLintel * 10

	act 'Play':KGOLpers = 1 & gt 'KGOLgame'

	act 'Delete a character':
		KGOLpers = 0
		gt $curloc
	end
}

minut += 1
gs 'stat'

if KFOnLineReaga > 0:
	'Welcome to the game Vertep World. You can play more <<KFOnLineReaga>> days'
	'At any time you can pay with a credit card that would extend the duration of the account.'
	'30 days 1000 rubles, on your credit card <<karta>>rubles'

	if KGOLpers > 0:
		'You can get in the game your character, or delete it'
		'<<$KGOLname>> <<$KGOLrace>> <<$KGOLklass>> <<KGOLlvl>> level'

		act 'Play':gt 'KGOLgame'

		act 'Delete a character':
			KGOLpers = 0
			gt $curloc
		end
	else
		'You can create a character'

		act 'Create a character':
			cls
			$KGOLname = input("Enter your nickname")

			if $KGOLname <= 0:msg 'Not valid nickname' & gt $curloc

			'Your nickname <<$KGOLname>>'
			'You must select the player race'

			act 'Man':$KGOLrace = 'Man' & KGOLrace = 1 & dynamic $KGOLklassvibor
			act 'Elf':$KGOLrace = 'Elf' & KGOLrace = 2 & dynamic $KGOLklassvibor
			act 'Zveroid':$KGOLrace = 'Zveroid' & KGOLrace = 3 & dynamic $KGOLklassvibor
		end
	end

	if karta >= 1000:
		act 'Payment akkuaunta':
			karta -= 1000
			KFOnLineReaga += 30
			gt $curloc
		end
	end

	if karta > 0:
		act 'Donat money in the game (1rub = 1moneta)':
			cla
			KGOLdonat = input("How much money do you want zadonatit?")

			if KGOLdonat <= 0 or KGOLdonat > karta:
				msg 'Not valid operation'
			else
				karta -= KGOLdonat
				KGOLmoney += KGOLdonat
				msg 'Transferred to your account <<KGOLdonat>> coins'
			end

			gt $curloc
		end
	end
else
	'Welcome to the online game Vertep World'
	'To play this game you have to pay 1, 000 rubles for 30 days with a credit card.'

	if karta >= 1000:
		act 'Payment':
			karta -= 1000
			KFOnLineReaga += 30
			gt $curloc
		end
	end
end

act 'Leave':gt 'Komp', 'start'

--- KGstart ---------------------------------
